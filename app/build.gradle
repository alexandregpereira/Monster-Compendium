plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    namespace 'br.alexandregpereira.hunter.app'
    compileSdkVersion project.findProperty('compileSdk')

    packagingOptions {
        jniLibs {
            excludes += ['META-INF/licenses/**']
        }
        resources {
            excludes += ['**/attach_hotspot_windows.dll', 'META-INF/licenses/**', 'META-INF/AL2.0', 'META-INF/LGPL2.1']
        }
    }

    defaultConfig {
        applicationId "br.alexandregpereira.hunter.app"
        minSdkVersion project.findProperty('minSdk')
        targetSdkVersion project.findProperty('targetSdk')
        versionCode 10
        versionName "1.2.5"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file('monster-keystore.jks')
            storePassword System.getenv("MONSTER_COMPENDIUM_KEYSTORE_PASSWORD")
            keyAlias 'monster'
            keyPassword System.getenv("MONSTER_COMPENDIUM_KEYSTORE_PASSWORD")
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }
    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.compose.compiler.get()
    }
}

dependencies {
    implementation project(':data:app')
    implementation project(':domain:app')
    implementation project(':event:folder-detail-event')
    implementation project(':event:folder-list-event')
    implementation project(':event:folder-preview-event')
    implementation project(':event:monster-detail-event')
    implementation project(':feature:folder-detail')
    implementation project(':feature:folder-insert')
    implementation project(':feature:folder-list')
    implementation project(':feature:folder-preview')
    implementation project(':feature:monster-compendium')
    implementation project(':feature:monster-detail')
    implementation project(':feature:monster-lore-detail')
    implementation project(':feature:search')
    implementation project(':feature:settings')
    implementation project(':feature:spell-detail')
    implementation project(':ui:core')

    implementation libs.bundles.viewmodel.bundle
    implementation libs.bundles.compose

    implementation libs.core.ktx
    implementation libs.appcompat
    implementation libs.material

    implementation libs.koin.android
    implementation libs.koin.compose
    testImplementation libs.bundles.unittest
    testImplementation libs.koin.test
}
